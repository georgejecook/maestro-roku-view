import "pkg:/source/roku_modules/mc/Utils.brs"

' /**
'  * @module KeyMixin
'  * @description Mixin method for helping with managing keypresses. Intended to be included in a base class, so that the super class can override the methods. Supports standard presses and long press gestures too.
'  */


function keyPressMixinInit()
  m.isKeyPressLocked = false
  m.longPressTimer = invalid
  m.longPressKey = ""
  m.isLongPressStarted = false
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ KEY HANDLING
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onKeyEvent(key as string, press as boolean) as boolean
  result = false
  if press
    'bs:disable-next-line
    if mc.isFunction(isAnyKeyPressLocked) and isAnyKeyPressLocked()
      return true
    else if m.isKeyPressLocked
      m.logWarn("All key presses are locked, due to isKeyPressLocked flag on ", m.top.id)
      return true
    end if

    if key = "down"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressDown) then result = onKeyPressDown() else result = false
    else if key = "up"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressUp) then result = onKeyPressUp() else result = false
    else if key = "left"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressLeft) then result = onKeyPressLeft() else result = false
    else if key = "right"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressRight) then result = onKeyPressRight() else result = false
    else if key = "OK"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressOK) then result = onKeyPressOK() else result = false
    else if key = "back"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressBack) then result = onKeyPressBack() else result = false
    else if key = "options"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressOption) then result = onKeyPressOption() else result = false
    else if key = "play"
      'bs:disable-next-line
      if mc.isFunction(onKeyPressPlay) then result = onKeyPressPlay() else result = false
    end if
  else if m.longPressKey <> ""
    toggleLongPressTimer(0)
    result = true
  end if

  if (result = invalid)
    result = false
  end if

  if result and press

    'bs:disable-next-line
    if mc.isFunction(getLongPressIntervalForKey) then longPressInterval = getLongPressIntervalForKey(key) else longPressInterval = 0
    if longPressInterval > 0
      m.logInfo("entering long press for key ", key)
      m.longPressKey = key
      toggleLongPressTimer(longPressInterval)
    end if
  else
    'bs:disable-next-line
    result = mc.isFunction(isCapturingAnyKeyPress) and isCapturingAnyKeyPress(key, press)
  end if

  if result = false and m.vm <> invalid
    result = m.vm.onKeyEvent(key, press)
  end if

  return result
end function

function toggleLongPressTimer(interval)
  if m.longPressTimer <> invalid
    m.longPressTimer.unobserveField("fire")
    m.longPressTimer = invalid
  end if

  if interval > 0
    m.longPressTimer = createObject("roSGNode", "Timer")
    m.longPressTimer.duration = interval
    m.longPressTimer.repeat = true
    m.longPressTimer.observeField("fire", mc.getFunctionName(onLongPressTimerFire))
    m.longPressTimer.control = "start"
  else if m.longPressKey <> ""
    m.logInfo("finishing longPress on key ", key)
    if m.isLongPressStarted and mc.isFunction(onLongPressFinish)
      'bs:disable-next-line
      onLongPressFinish(m.longPressKey)
    end if
    m.longPressKey = ""
    m.isLongPressStarted = false
  end if
end function

function onLongPressTimerFire()
  if m.isLongPressStarted
    'bs:disable-next-line
    if mc.isFunction(onLongPressUpdate) and not onLongPressUpdate(m.longPressKey)
      m.logInfo("long press was cancelled by the onLongPressUpdate call")
      if mc.isFunction(toggleLongPressTimer)
        toggleLongPressTimer(0)
      end if
    end if
  else
    'bs:disable-next-line
    if mc.isFunction(onLongPressStart) and not onLongPressStart(m.longPressKey)
      m.logInfo("long press was rejected by onLongPressStart call")
      toggleLongPressTimer(0)
    else
      m.logInfo("long press is accepted : starting for key ", m.longPressKey)
      m.isLongPressStarted = true
    end if
  end if
end function


