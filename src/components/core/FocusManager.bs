import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/roku_modules/mc/Utils.brs"

' /**
'  * @module FocusManager
'  * @description Source of truth about application focus
'  */

function init() as void
  log_registerLogger("FocusManager")
  m.top.observeField("_debugM", "onDebugMChange")
  m.focusTimer = m.top.findNode("focusTimer")
  m.wasShown = false
  m.isKeyPressLocked = false
  m.lockedNode = invalid
end function

function onDebugMChange()
  log_logInfo("dumping m for debugging purposes")
  m.top._m = m
end function


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ public api
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function setFocusOn(node, isForced = false) as void
  ' log_logMethod("setFocusOn", node)

  if m.top.isLocked
    if isForced
      log_logWarn("overriding setting log_locked focus (locked reason:", m.top.lockReason, ")")
    else
      log_logWarn("cannot set focus, it is log_locked - reason :", m.top.lockReason)
      return 
    end if
  end if

  if isValid(node)
    parents = getParents(node)

    if isInScene(parents)

      if m.top.isLogging
        log_logInfo("setting focus to ", node.subType(), ".", node.id)
      end if

      _setFocusOn(node, parents)
    else
      log_logError("set focus to an off scene node! - in future this will set the focus to a backup-context node, to stop illegal focus scenarios")
      _setFocusOn(node, parents)
    end if
  end if

end function

function unsetFocusOn(node)
  log_logInfo("manualy unsetting focus on node")
  node.setFocus(false)
end function

function setFocusLocked(node, reason = "")

  if node <> invalid
    setFocusOn(node, true)
    m.lockedNode = node
    log_logInfo("locking focus due to reason", reason)
    m.focusTimer.control = "start"
  else
    log_logInfo("unlocking focus - previous log_lock reason:", m.top.lockReason)
    m.focusTimer.control = "stop"
    m.lockedNode = invalid
  end if

  m.top.lockReason = reason
  m.top.isLocked = m.lockedNode <> invalid
end function

function onFocusTimerFire()
  ' log_logInfo("onFocusTimerFire")
  if m.focusedNode <> invalid
    m.focusedNode.setFocus(true)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ private impl
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function _setFocusOn(node, parents) as void

  currentNode = m.top.focusedNode
  isCurrentNode = false
  if (node = invalid and currentNode = invalid) or (node <> invalid and node.isSamenode(currentNode))
    log_logInfo("setting focus on same node - ignoring")
    if node <> invalid
      if node.doesExist("isFocused")
        node.isFocused = true
      end if
      if not node.isInFocusChain()
        isCurrentNode = true
      end if
    end if
    if not isCurrentNode
      return
    end if
  end if

  if currentNode <> invalid and not isCurrentNode
    ' log_logInfo("setting focus on new node - unsetting focus on", currentNode.id) 
    if currentNode.doesExist("isFocused") and currentNode.isFocused = true
      currentNode.isFocused = false
    end if

    setFieldOnNodes(m.parents, "isChildFocused", false)
  end if

  m.top.focusedNode = node
  m.parents = parents

  if node <> invalid
    if node.doesExist("drawFocusFeedback")
      previousDrawFocusFeedback = node.drawFocusFeedback
      node.drawFocusFeedback = false
    end if

    node.setFocus(true)

    if node.doesExist("drawFocusFeedback")
      node.drawFocusFeedback = previousDrawFocusFeedback
    end if

    if node.doesExist("isFocused")
      ' log_logInfo("setting node as focused, and telling parents", node.id)
      node.isFocused = true
    end if
    setFieldOnNodes(parents, "isChildFocused", true)
  else
    log_logError("set focus to invalid node! - in future this will set the focus to a backup-context node, to stop illegal focus scenarios")
  end if
end function

function getParents(node)
  parent = node.getParent()
  parents = []
  while (parent <> invalid)

    if parent.subType() = "ModelLocator"
      log_logWarn("trying to focus on a view that is not yet on the view hierarchy")
      return []
    end if
    parents.push(parent)
    parent = parent.getParent()
  end while

  return parents
end function

function isValid(node)

  if MU.isUndefined(node)
    log_logError("asked to focus on undefined node")
    return false
  else if node = invalid
    log_logError("asked to focus on invalid node")
    return false
  else if type(node) <> "roSGNode"
    log_logError("asked to focus on non rsgnode")
    return false
  end if

  return true
end function 

function isInScene(parents)

  if parents.count() = 0
    log_logError("asked to focus on node with no parents")
    return false
  end if

  lastParent = parents[parents.count() - 1]

  if not lastParent.isSubType("Scene")
    log_logError("node is not in the scene")
    return false
  end if

  return true
end function

function setFieldOnNodes(nodes, field, value)
  ' log_logInfo("--------------------------------START") 
  for i = 0 to nodes.count() - 1
    node = nodes[i]
    ' log_logInfo(i, "id", node.id)
    if node.doesExist(field) and node[field] <> value
      ' log_logInfo(field, "node[field]", node[field], value)
      node[field] = value
    end if

  end for
  ' log_logInfo("--------------------------------END") 

end function
