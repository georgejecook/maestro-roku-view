import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/roku_modules/mc/Utils.brs"

' /**
'  * @module FocusManager
'  * @description Source of truth about application focus
'  */

function init() as void
  log_registerLogger("FocusManager")
  m.top.observeField("_debugM", mc.getFunctionName(onDebugMChange))
  m.focusTimer = m.top.findNode("focusTimer")
  m.wasShown = false
  m.isKeyPressLocked = false
  m.lockedNode = invalid
end function

function onDebugMChange()
  m.logInfo("dumping m for debugging purposes")
  m.top._m = m
end function


'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ public api
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function setFocusOn(node, isForced = false) as void
  ' m.logMethod("setFocusOn", node)

  if m.top.isLocked
    if isForced
      m.logWarn("overriding setting m.locked focus (locked reason:", m.top.lockReason, ")")
    else
      m.logWarn("cannot set focus, it is m.locked - reason :", m.top.lockReason)
      return 
    end if
  end if

  if isValid(node)
    parents = getParents(node)

    if isInScene(parents)

      if m.top.isLogging
        m.logInfo("setting focus to ", node.subType(), ".", node.id)
      end if

      _setFocusOn(node, parents)
    else
      m.logError("set focus to an off scene node! - in future this will set the focus to a backup-context node, to stop illegal focus scenarios")
      _setFocusOn(node, parents)
    end if
  end if

end function

function unsetFocusOn(node)
  m.logInfo("manualy unsetting focus on node")
  node.setFocus(false)
end function

function setFocusLocked(node, reason = "")

  if node <> invalid
    setFocusOn(node, true)
    m.lockedNode = node
    m.logInfo("locking focus due to reason", reason)
    m.focusTimer.control = "start"
  else
    m.logInfo("unlocking focus - previous m.lock reason:", m.top.lockReason)
    m.focusTimer.control = "stop"
    m.lockedNode = invalid
  end if

  m.top.lockReason = reason
  m.top.isLocked = m.lockedNode <> invalid
end function

function onFocusTimerFire()
  ' m.logInfo("onFocusTimerFire")
  if m.focusedNode <> invalid
    m.focusedNode.setFocus(true)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ private impl
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function _setFocusOn(node, parents) as void

  currentNode = m.top.focusedNode
  isCurrentNode = false
  if (node = invalid and currentNode = invalid) or (node <> invalid and node.isSamenode(currentNode))
    m.logInfo("setting focus on same node - ignoring")
    if node <> invalid
      if node.doesExist("isFocused")
        node.isFocused = true
      end if
      if not node.isInFocusChain()
        isCurrentNode = true
      end if
    end if
    if not isCurrentNode
      return
    end if
  end if

  if currentNode <> invalid and not isCurrentNode
    ' m.logInfo("setting focus on new node - unsetting focus on", currentNode.id) 
    if currentNode.doesExist("isFocused") and currentNode.isFocused = true
      currentNode.isFocused = false
    end if

    setFieldOnNodes(m.parents, "isChildFocused", false)
  end if

  m.top.focusedNode = node
  m.parents = parents

  if node <> invalid
    if node.doesExist("drawFocusFeedback")
      previousDrawFocusFeedback = node.drawFocusFeedback
      node.drawFocusFeedback = false
    end if

    node.setFocus(true)

    if node.doesExist("drawFocusFeedback")
      node.drawFocusFeedback = previousDrawFocusFeedback
    end if

    if node.doesExist("isFocused")
      ' m.logInfo("setting node as focused, and telling parents", node.id)
      node.isFocused = true
    end if
    setFieldOnNodes(parents, "isChildFocused", true)
  else
    m.logError("set focus to invalid node! - in future this will set the focus to a backup-context node, to stop illegal focus scenarios")
  end if
end function

function getParents(node)
  parent = node.getParent()
  parents = []
  while (parent <> invalid)

    if parent.subType() = "ModelLocator"
      m.logWarn("trying to focus on a view that is not yet on the view hierarchy")
      return []
    end if
    parents.push(parent)
    parent = parent.getParent()
  end while

  return parents
end function

function isValid(node)

  if mc.isUndefined(node)
    m.logError("asked to focus on undefined node")
    return false
  else if node = invalid
    m.logError("asked to focus on invalid node")
    return false
  else if type(node) <> "roSGNode"
    m.logError("asked to focus on non rsgnode")
    return false
  end if

  return true
end function 

function isInScene(parents)

  if parents.count() = 0
    m.logError("asked to focus on node with no parents")
    return false
  end if

  lastParent = parents[parents.count() - 1]

  if not lastParent.isSubType("Scene")
    m.logError("node is not in the scene")
    return false
  end if

  return true
end function

function setFieldOnNodes(nodes, field, value)
  ' m.logInfo("--------------------------------START") 
  for i = 0 to nodes.count() - 1
    node = nodes[i]
    ' m.logInfo(i, "id", node.id)
    if node.doesExist(field) and node[field] <> value
      ' m.logInfo(field, "node[field]", node[field], value)
      node[field] = value
    end if

  end for
  ' m.logInfo("--------------------------------END") 

end function
