import "pkg:/source/roku_modules/mc/Utils.brs"

function init()
  m.top.observeFieldScoped("fontKey", "onFontKeyChange")
  m.top.observeFieldScoped("style", "onStyleChange")
  onFontKeyChange()
end function

function onFontKeyChange()

  if m.top.fontKey <> invalid
    parts = m.top.fontKey.split(",")
    if parts <> invalid and parts.count() > 0

      if parts.count() = 4 and m.global.fontManager <> invalid
        m.top.font = m.global.fontManager@.getFontWithKey(m.top.fontKey)
      else
        m.top.font = "font:" + parts[0] + "SystemFont"
      end if
      if parts.count() > 1
        if m.top.font <> invalid
          m.top.font.size = parts[1].toInt()
        end if
      end if
    end if

  end if
end function

function onStyleChange()
  m.top.setFields(m.top.style)
end function
