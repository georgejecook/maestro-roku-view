'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Initialization
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function init()
  m.logMethod("init")
  m.top.navController = m.top.findNode("navController")
  initializeView(m.top.navController)
  m.wasRootViewDisplayed = false
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Public API
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function reset(args = invalid)
  m.logMethod("reset")
  resetNavControllerToRoot()

  if m.top.navController.currentView <> invalid
    m.top.navController.currentView@.reset()
  end if

  if m.top.hasFocus()
    setFocus(m.top.navController)
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Private impl
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function createFirstScreen()
  m.logMethod("createFirstScreen")
  if m.top.rootView <> invalid
    if m.top.rootView <> invalid
      push(m.top.rootView)
      m.wasRootViewDisplayed = true
    else
      m.logError("no screen ")
    end if
  else
    m.logError("rootView was invalid!")
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Key handling
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onKeyPressBack() as boolean
  m.logMethod("onKeyPressBack")

  if m.top.navController.numberOfViews > 1
    pop()
    setFocus(m.top.navController)
    return true
  end if

  return false
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function onShow()
  m.logMethod("onShow")

  if m.wasRootViewDisplayed
    reset()
  else
    createFirstScreen()
  end if
  m.top.navController.visible = true
end function

function onHide()
  m.logMethod("onHide")
  m.top.navController.visible = false
  reset()
end function

function onGainedFocus(isSelfFocused)
  ' m.logMethod("onGainedFocus ", isSelfFocused)
  if isSelfFocused
    setFocus(m.top.navController)
  end if
end function
